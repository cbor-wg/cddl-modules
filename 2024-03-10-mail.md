# Modules vs. Sockets

This is a short discussion of what I consider to be the remaining
issue that we have to solve for draft-ietf-cbor-cddl-modules.

TL;DR: I'm afraid this works better if it is actually read as a whole.

I haven't made a github issue yet because I'd like to have some
discussion on the mailing list first.

1. contents
{:toc}

## Background: From ABNF =/ to CDDL /= and //=

### ABNF's "Incremental Alternatives"

ABNF (RFC5234) has a way to build up a choice from multiple lines that
almost look like rules, called "Incremental Alternatives" ([Section
3.3][1]):

         ruleset     =  alt1 / alt2

         ruleset     =/ alt3

         ruleset     =/ alt4 / alt5

    is the same as specifying

         ruleset     =  alt1 / alt2 / alt3 / alt4 / alt5


RFC 4695, RTP Payload Format for MIDI from November 2006, has this as
a real-world example:


    ; Parameters defined in Appendix C.1
    param-assign =   ("cm_unused="  (([channel-list] command-type [f-list]) / sysex-data))
    param-assign =/  ("cm_used="    (([channel-list] command-type [f-list]) / sysex-data))

    ; Parameters defined in Appendix C.2
    param-assign =/  ("j_sec="      ("none" / "recj" / *ietf-extension))
    param-assign =/  ("j_update="   ("anchor" / "closed-loop" / "open-loop" / *ietf-extension))
    param-assign =/  ("ch_default=" (([channel-list] chapter-list [f-list]) / sysex-data))

etc.

So the =/ feature is used to piece together a rule with many
alternatives from spec pieces distributed over a number of sections.

Note that there always is a plain = rule before we go into the =/ rules.
Bill Fenner's ABNF tool has this to say if there isn't:

    $ echo a =/ '"a"' | bap
    stdin(1:0): error: Rule a does not yet exist; treating /= as =

### CDDL's "additional alternatives"

CDDL was inspired by ABNF's syntax, so we tried to adapt this feature,
with three changes:

1. CDDL has type choices (/) and group choices (//), so we needed to
   add an "additional alternatives" feature to both.
2. We used the opportunity to turn around the /s and the =, so we got
   /= for adding an alternative to a type choice and //= to a group
   choice.
3. We made this ominous extension ([Section 2.2 of RFC 8610][2]):

    It is not an error if a name is first used with a "/=" or "//="
    (there is no need to "create it" with "=").

## Sockets/Plugs

With these adaptations, we then added the convention of [sockets/plugs][3]
to CDDL, a way to make names stand out that are intended to employ
additional alternatives (file name comments added to the example in
[Section 3.9][3]):

    ; a file tcp-example.cddl

    tcp-header = {seq: uint, ack: uint, * $$tcp-option}

    ; later, in a different file tcp-example-sack.cddl

    $$tcp-option //= (
    sack: [+(left: uint, right: uint)]
    )

    ; and, maybe in another file tcp-example-sackperm.cddl

    $$tcp-option //= (
    sack-permitted: true
    )

The examples in Section 3.9 of RFC8610 are chosen so they do not need
additional rules, but that would be a quite exceptional usage for
modules:
If the three parts of this example that are split by the comments are
modules, one might expect some of the modules to make additional
definitions.
When these modules are imported/import others, the rules for modules
handle the additional names coming in fine:
Any names that are not defined in the importing module will be sought
out in the imported module and made part of the import.

But after including the module defining `sack`, $$tcp-option is no
longer undefined!  So an import of the module for `sack-permitted` is
going to fail actually importing the rule adding an alternative.

Note that an `include` works just fine (but might import extraneous
rules); the problem is with the logic in `import` that only copies
over the rules that are actually needed (transitive closure of the
unsatisfied references).

## Extending the module concept to accommodate sockets/plugs

There are several ways to mend this, and this is why it probably makes
sense to have a discussion first.  I'm trying to break this up into
individual decisions; in practice of course these interact.

### Decision one: (Pre)Fixing the socket convention

The socket naming convention does not interact well with the namespace
prefixing we use in draft-ietf-cbor-cddl-modules:

    ;# import tcp-example-sack as sack

➔

    sack.$$tcp-option //= (
    sack: [+(left: uint, right: uint)]
    )

Ouch.

So we probably need to adapt the prefixing mechanism to know about the
socket convention; probably simply by moving the $ signs to the start
of the prefixed name:

    $$sack.tcp-option //= (
    sack: [+(left: uint, right: uint)]
    )

(decision 1: do we handle this issue by this little sleight of hand,
or is there a better alternative?)

> We could also decide that socket names are always "global", i.e. they
> don't participate in the prefixing game at all.
>
> * Pro: fewer surprises when piecing together a socket from multiple modules
> * Con: No isolation; do we have strong enough conventions to avoid
>   sockets leaking/miraculously merging between different module trees?
>
> Note that aliasing by a simple assignment rule doesn't in general work
> for sockets -- this needs /= or //= (the current implementation
> doesn't know about this and Decision 1 above -- it occasionally spits
> out "** warning: plain assignment of `coswid.$version-scheme` to type
> socket `$version-scheme`"):
>
>     ;# import $version-scheme from rfc9393 as coswid
>     a = 1
>
> ➔
>
>     a = 1
>     $version-scheme = coswid.$version-scheme
>     coswid.$version-scheme /= coswid.multipartnumeric / coswid.multipartnumeric-suffix / coswid.alphanumeric / coswid.decimal / coswid.semver / int / text
>     coswid.multipartnumeric = 1
>     coswid.multipartnumeric-suf...



### Decision two: just sockets or all additional alternatives?

Socket names are a convention that helps extension points in a CDDL
model stand out to humans (*and* allows tools to be smart about them).
We could extend whatever we come up with in the module system to work
with any use of Section 2.2.2 additional alternatives.

Note that

    a = 1
    a /= 2

feels somewhat different from the more socket-like

    a /= 1
    a /= 2

so maybe we could detect the latter and handle them like sockets, which
use the same pattern, instead of relying on the name.
But maybe it is good to explicitly invoke any socket-related module
mechanics by using a special name?

(decision 2: react on the socket naming convention or on the use of
additional alternatives without an initial alternative given?)

In the next sections, we will try to make the discussion independent
of this decision by using the term "sockets" either for (1) rules named
with the socket naming convention in the first case or for (2) rules that
additional alternatives are added to without an initial alternative
given in the second case.

### Decision three: how to scope an import

[Section 2.5 of draft-ietf-cbor-cddl-modules][4] says (paraphrased):
`import` is similar to `include` but includes only those rules from
the module that are referenced from the importing module, implicitly
(unsatisfied rulename references) or explicitly (clause ending in
"from"), including the rules that are referenced from these rules,
transitively.

How does this work?

The referencing (importing) module has zero or more unsatisfied
references, i.e., rulenames mentioned in existing rules but not defined.

Without a `from` clause, the import uses that set as what I'll call
here the "activated" set.

If the importer does not know that the imported module defines plugs
for a socket that will later be used/added to in other modules, we
fail to make the connection.

Alternatively, the importing module can give a `from` clause,
determining the "activated" set precisely.  Let's try how much this helps.
(Re the question at the end of Decision one: The name
`$version-scheme` used in RFC 9393 is a good counterexample
against trusting modules to sort out their socket names for global
uniqueness...)

    ;# import $version-scheme from rfc9393
    a = 1

(where a = 1 symbolizes the rest of the importing module) leads to:

    a = 1
    $version-scheme /= multipartnumeric / multipartnumeric-suffix / alphanumeric / decimal / semver / int / text
    multipartnumeric = 1
    multipartnumeric-suffix = 2
    alphanumeric = 3
    decimal = 4
    semver = 16384

What worked well here was that the dependent rules were imported as well.

Note that this doesn't work too well when the socket springs into
existence in a module just because it is used:

    ;# import $$coswid-extension from rfc9393
    a = 1
    ➔
    *** imported rule "$$coswid-extension" not in rfc9393.cddl
    a = 1

So to use import with `from` to obtain sockets from an imported module

* may require the importing module to know which sockets the imported
  module creates
* can lead to error messages for sockets that don't have plugs yet.

#### Redefining "activation"

To make `import` more useful without the increased coupling caused by
requiring this knowledge, we probably need a different concept of
"activation".

Let's discuss this as a module M that imports a module L and a module
K.  L and K use/add to socket $version-scheme.

If M does use the socket `$version-scheme` (which implies a stronger
coupling already), we could solve this by simply putting all socket
names used by M into the activated set when M imports another module.

However, if M is not aware of `$version-scheme`, all sockets in the
first imported module L would need to be activated just in case
another module (like K) is later imported that employs
`$version-scheme` as well.

L could "`export`" `$version-scheme` if it wants to enable the
cooperation with K behind M's back.

*...insert more wisdom here...*

[1]: https://www.rfc-editor.org/rfc/rfc5234.html#section-3.3
[2]: https://www.rfc-editor.org/rfc/rfc8610.html#section-2.2.2
[3]: https://www.rfc-editor.org/rfc/rfc8610.html#section-3.9
[4]: https://www.ietf.org/archive/id/draft-ietf-cbor-cddl-modules-02.html#section-2.5
